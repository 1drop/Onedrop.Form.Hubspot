prototype(Onedrop.Form.Hubspot:FormRuntimeFactory) {
    @class = 'Onedrop\\Form\\Hubspot\\FusionObjects\\FormRuntimeFactory'

    noForm = Neos.Fusion:Tag {
        tagName = 'div'
        attributes {
            class = 'onedrophubspot_noform'
        }
        content = 'Please select a hubspot form in the element inspector'
        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping
    }
}

prototype(Onedrop.Form.Hubspot:Content.Form) < prototype(Neos.Neos:ContentComponent) {
    formWrapperAttributes = Neos.Fusion:RawArray {
        data-ajax = 'ajax-form'
        data-ajax-uri = Neos.Fusion:UriBuilder {
            package = 'Onedrop.Form.Hubspot'
            controller = 'AjaxForm'
            action = 'submit'
            format = 'html'
        }
    }

    content = Onedrop.Form.Hubspot:FormRuntimeFactory {
        identifier = ${q(node).property('formIdentifier')}
    }

    renderer = afx`
        <div data-ajax={props.formWrapperAttributes.data-ajax} data-ajax-uri={props.formWrapperAttributes.data-ajax-uri}>
            {props.content}
        </div>
    `

    @cache {
        mode = 'uncached'
        context {
            1 = 'node'
            2 = 'documentNode'
        }
    }

    @exceptionHandler = 'Onedrop\\Form\\Hubspot\\FusionObjects\\ExceptionHandler'
}

ajaxForm = Onedrop.Form.Hubspot:Content.Form
